grammar dk.sdu.mmmi.mdsd.Math with org.eclipse.xtext.common.Terminals

generate math "http://www.sdu.dk/mmmi/mdsd/Math"

Program:
	'program' name=ID external+=External* mathExp=MathExp
;

External:
	'external' name =ID '(' (argumentLeft = ID)? (',' argumentsRight += ID)* ')'
;

MathExp:
	variables += VarBinding*
;

VarBinding:
	'var' name=ID '=' expression=Exp
;

Exp returns Expression:
	Factor (( {Plus.left=current} '+' | {Minus.left=current}  '-' ) right=Factor)*
;

Factor returns Expression:
	Primary (({Mult.left=current} '*' | {Div.left=current}  '/'  ) right=Primary)*
;

Primary returns Expression:
	{MathNumber} value=INT  | Par |  VariableUse | LetBinding | ExternalUse
;

Par returns Expression:
	{Par} '(' exp = Exp ')' 
;

ExternalUse:
	name =ID '(' (argumentLeft = Exp)? (',' argumentsRight += Exp)* ')'
;

LetBinding:
	'let' name=ID '=' binding=Exp 'in' body=Exp 'end'
;

Binding:
	VarBinding | LetBinding
;

VariableUse:
	ref = [Binding]
;
